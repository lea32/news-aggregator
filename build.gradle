group 'ru.leasoft.challenge'
version '1.0.0-RELEASE'

apply plugin: 'groovy'
apply plugin: 'application'

sourceCompatibility = 1.8

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

task assembleDefaultConfig {
    def conf = file("$buildDir/conf")
    def defaultParsersDir = file("$buildDir/conf/parsers")
    outputs.dir conf
    doLast {
        conf.mkdirs()
        defaultParsersDir.mkdirs()
        copy {
            from 'default.config'
            into conf
            rename('default.config', 'aggregator')
        }

        copy {
            from 'parsers'
            into defaultParsersDir
        }
    }
}

distributions {
    main {
        contents {
            from(assembleDefaultConfig) {
                into "conf"
            }

            def tempDirBase = new File('/tmp/news-aggregator-dummy')
            tempDirBase.mkdirs()
            def tempDir = new File(tempDirBase.absolutePath + '/tmp')
            tempDir.mkdirs()
            from {tempDirBase}
        }
    }
}

ext {
    springVersion = '4.3.11.RELEASE'
    hibernateVersion = '5.2.11.Final'
}

mainClassName = 'ru.leasoft.challenge.aggregator.Aggregator'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':web')

    compile 'org.codehaus.groovy:groovy-all:2.4.12'

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    compile group: 'io.undertow', name: 'undertow-core', version: '1.4.20.Final'
    compile group: 'io.undertow', name: 'undertow-servlet', version: '1.4.20.Final'

    compile group: 'org.springframework', name: 'spring-web', version: springVersion
    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
    compile group: 'org.springframework', name: 'spring-tx', version: springVersion
    compile group: 'org.springframework', name: 'spring-orm', version: springVersion
    compile group: 'org.springframework', name: 'spring-context-support', version: springVersion

    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-c3p0', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateVersion
    compile 'org.hibernate:hibernate-search-orm:5.8.1.Final'

    compile 'org.hibernate:hibernate-validator:6.0.2.Final'
    compile 'org.glassfish:javax.el:3.0.1-b08'

    compile group: 'org.flywaydb', name: 'flyway-core', version: '3.2.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.33'

    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'

    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.netflix.hystrix:hystrix-core:1.5.12'
    compile 'org.aspectj:aspectjweaver:1.8.6'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.7.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
    testCompile group: 'org.springframework', name: 'spring-test', version: springVersion
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.187'
}


